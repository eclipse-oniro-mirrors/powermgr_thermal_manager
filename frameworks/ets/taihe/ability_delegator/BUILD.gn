# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../../thermalmgr.gni")

copy_taihe_idl("copy_thermal_manager") {
  sources = [ "idl/ohos.thermal.taihe" ]
}

subsystem_name = "powermgr"
part_name = "thermal_manager"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_thermal_manager" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.thermal.ani.cpp",
    "$taihe_generated_file_path/src/ohos.thermal.abi.c",
  ]
}

config("thermal_private_config") {
  include_dirs = [ "${thermal_service_zidl}/include" ]
}

taihe_shared_library("thermal_manager_taihe_native") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "$taihe_generated_file_path"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [ "./include" ]
  configs = [
    "${utils_path}:utils_config",
    ":thermal_private_config",
    "${utils_path}:coverage_flags",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.thermal.impl.cpp",
    "src/thermal_level_callback.cpp",
  ]
  deps = [
    ":run_taihe",
    "${thermal_inner_api}:thermalmgr_listener",
    "${thermal_inner_api}:thermalsrv_client",
  ]
  external_deps = [
    "bundle_framework:appexecfwk_base",
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "power_manager:powermgr_client",
  ]
}

generate_static_abc("thermal_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.thermal.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/thermal_abc.abc"
  dependencies = [ ":run_taihe" ]
}

generate_static_abc("thermal_test_abc") {
  base_url = "$thermal_frameworks/ets/taihe/ability_delegator/test"
  files = [ "$thermal_frameworks/ets/taihe/ability_delegator/test/test_main.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/thermal_test_abc.abc"
}

ohos_prebuilt_etc("thermal_etc") {
  source = "$target_out_dir/thermal_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":thermal_abc" ]
}